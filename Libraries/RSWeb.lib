RSWeb:
	If (ItsLocked=1) 
		return

	Loop, 8
	{
		IniRead, ws%A_Index% , settings.ini, Web, ws%A_Index%
	}

	webSearchVar=
	FastSearching:=false
	WebLT := WebLT ? false : true

	ChoosingWebsite:= (SearchBased) ? false : true

	If WebLT=0
	{
		GoSub WebLClose
		return
	}
	If WebLT=1
	{
		If (A_ScreenWidth<2000)
		{
			zfont=12
			amultiplier=1
		}
		If (A_ScreenWidth<1800)
		{
			zfont=11
			amultiplier=1
		}
		If (A_ScreenWidth<1600)
		{
			zfont=10
			amultiplier=0.92
		}
		If (A_ScreenWidth<1400)
		{
			zfont=10
			amultiplier=0.87
		}
		If (A_ScreenWidth<1050)
		{
			zfont=9
			amultiplier=0.80
		}

		Gui, WeBL:Destroy
		Gui, WebL:New, +hwndWebL
		Gui, -Border -Caption +AlwaysOnTop +LastFound +ToolWindow

		If SearchBased
		{
			guiw:=520*amultiplier
			guih:=46
			Gui, Color, 172326, 172326
			Gui, Font, w1000 s12 c909090, Segoe UI Bold
			Gui, Add, Text, x16 y11 h45 w440 vSearchLabel +Hidden, Google:
			Gui, Font, w1000 s12 cf9f9f9, Segoe UI Bold
			Gui, Add, Edit, % "x14 y11 w" guiw-80 " h35 hwndWebSearch vwebSearchVar -E0x200 -VScroll", %webSearchVar%
			Gui, Font, w700 s22 ceeeeee, Segoe UI Black
			Gui, Add, Button, % "x+2 y-5 h45 w55 gStartSearch hwndSearchBut1 +Default", •••
			Opt1 := [6, 0x172326, 0x172326, 0x909090]
			Opt2 := [ , 0x172326, 0x172326, 0xffffff]
		}
		else
		{
			guiw:=720*amultiplier
			guih:=204*amultiplier-25
			Gui, Color, 1e1e1e, 333333
			Gui, Font, w700 s%zfont% ceeeeee, Segoe UI Black
			Gui, Add, Progress, % "x-1 y-1 w" guiw " h22 Background333333 Disabled hwndHPROG"
			Gui, Add, Edit, % "x0 y4 w" guiw-45 " h25 hwndWebSearch vwebSearchVar -E0x200 +Center -VScroll", %webSearchVar%
			Gui, Add, Button, % "x+0 h25 w45 gStartSearch hwndSearchBut1 +Default", Go
			Opt1 := [6, 0x333333, 0x333333, 0xD0D0D0]
			Opt2 := [ , 0x333333, 0x333333, 0xffffff]
		}

		Opt4 := [0, 0xC0A0A0A0, , 0xC0606000]
		ImageButton.Create(SearchBut1, Opt1, Opt2, "", Opt4)
		buth:=(guih)/2-15
		butw:=(guiw)/4
		RSWebGuiWidth:=guiw
		RSWebGuiHeight:=guih

		If !SearchBased
		{
			Opt1 := [6, 0x232323, 0x232323, "White"]
			Opt2 := [ , 0x272727, 0x262626, 0xffffff]
			Opt4 := [0, 0xC0A0A0A0, , 0xC0606000]
			Gui, Add, Button, hwndSCHYT1 gws1 vwebname1 x0 y+0 w%butw% h%buth%, %WebName1%
			ImageButton.Create(SCHYT1, Opt1, Opt2, "", Opt4)

			Gui, Add, Button, hwndSCHYT2 gws2 vwebname2 x+0 w%butw% h%buth%, %WebName2%
			ImageButton.Create(SCHYT2, Opt1, Opt2, "", Opt4)

			Gui, Add, Button, hwndSCHYT3 vwebname3 gws3 x+0 w%butw% h%buth%, %WebName3%
			ImageButton.Create(SCHYT3, Opt1, Opt2, "", Opt4)

			Gui, Add, Button, hwndSCHYT4 vwebname4 gws4 x+0 w%butw% h%buth%, %WebName4%
			ImageButton.Create(SCHYT4, Opt1, Opt2, "", Opt4)

			Gui, Add, Button, hwndSCHYT5 vwebname5 gws5 x0 y+0 w%butw% h%buth%, %WebName5%
			ImageButton.Create(SCHYT5, Opt1, Opt2, "", Opt4)

			Gui, Add, Button, hwndSCHYT6 vwebname6 gws6 x+0 w%butw% h%buth%, %WebName6%
			ImageButton.Create(SCHYT6, Opt1, Opt2, "", Opt4)

			Gui, Add, Button, hwndSCHYT7 vwebname7 gws7 x+0 w%butw% h%buth%, %WebName7%
			ImageButton.Create(SCHYT7, Opt1, Opt2, "", Opt4)

			Gui, Add, Button, hwndSCHYT8 vwebname8 gws8 x+0 w%butw% h%buth% , %WebName8%
			ImageButton.Create(SCHYT8, Opt1, Opt2, "", Opt4)
		}

		If SearchBased
		{
			WinSet, Transparent, 245
			WinSet, Region, % "0-0 w" guiw*dpi " h" guih*dpi " r12-12", ahk_id %WebL%
		}
		else
		{
			WinSet, Transparent, 0
			WinSet, Region, % "0-0 w" guiw*dpi " h" guih*dpi " r18-18", ahk_id %WebL%
		}

		If RSWebWithSearchFocused || SearchBased
		{
			Gui, Show, h%guih% w%guiw% NoActivate, %ScriptName% Speed Dial
			WinActivate
			SetTimer, CheckFastSearchInactive, 20
		}
		else
			Gui, Show, h%guih% w%guiw% NoActivate, %ScriptName% Speed Dial
		ShAutoComplete(WebSearch)
		If !SearchBased
			FadeIn(WebL)
		HideFocusBorder(WebL)
	}
return

CheckFastSearchInactive:
	If !WinActive("ahk_id" . WebL) && !WinActive("ahk_id" . WebEdit)
		GoSub WebLClose
return

WebLClose:
WebLGuiEscape:
	SetTimer, CheckFastSearchInactive, Off
 	WebLT := false
	Gui, Tip:Destroy
	FadeOut(WebL)
	currentKeyword=
	ChoosingWebsite:=false
	SearchBased:=false
	RSWebWithSearchFocused:=false
return

WebLGuiContextMenu:
	if !A_GuiControl
		return
	
	if (currentKeyword="g") || (currentKeyword="y") || (currentKeyword="maps") || (currentKeyword="tr")
	{
		RSNotify("Not possible")
		return
	}

	if FastSearching
		width=400
	else
		width=300

	Loop, 8
	{
		If A_GuiControl=webname%A_Index%
		{
			wnumb=%A_Index%
			break
		}
	}

 	Gui, WebEdit:New, +hwndWebEdit
	Gui, +LastFound
	WinSet, Transparent, 0
	Gui, Margin, 0, 0
	Gui, -Caption
	Gui, Color, 252525, 333333
	Gui, Font, s10 Bold cEEEEEE, Segoe UI
	Gui, Add, Progress, % "x-1 y-1 w" width " h26 Background1f1f1f Disabled hwndHPROG"
	Gui, Add, Text, x0 y4 w%width% Center BackgroundTrans +0x200 cb1b1b1, % (FastSearching) ? "Edit Quick Search Query" : "Edit Website"

	Opt1 := [6, 0x252525, 0x252525, "White"]
	Opt2 := [ , 0x252525, 0x444444, 0xffffff]
	Opt4 := [0, 0xC0A0A0A0, , 0xC0606000]

	if FastSearching
	{
		Gui, Add, Edit, % "x29 y+10 w60 h20 hwndEdit265 -E0x200 vnewkeyword +Center -VScroll", %currentKeyword%
		Gui, Add, Edit, % "x+5 w" width-58-65 " h20 hwndEdit264 -E0x200 vnewLabel +Center -VScroll", % %currentKeyword%Label
		Gui, Add, Edit, % "x29 y+6 w" width-58 " h20 hwndEdit266 -E0x200 vnewUrl" " +Center -VScroll", % %currentKeyword%url

		Gui, Add, Button, % "x30 y+6 w" width/3-20 " +default h27 gEditWebsites hwndBut1 vSaveBtn +Center +Default", Save
		ImageButton.Create(But1, Opt1, Opt2, "", Opt4)

		Gui, Add, Button, % "x+0 w" width/3-20 " h27 gDeleteCustomQuery hwndBut1 +Center +Default", Delete
		ImageButton.Create(But1, Opt1, Opt2, "", Opt4)

		Gui, Add, Button, % "x+0 w" width/3-20 " h27 gExitWebsites hwndBut2 +Center", Cancel
		ImageButton.Create(But2, Opt1, Opt2, "", Opt4)
	}
	else
	{
		Gui, Add, Edit, % "x29 y+12 w242 h18 hwndEdit264 v" A_GuiControl " -E0x200 +Center -VScroll", % %A_GuiControl%
		Gui, Add, Edit, % "x29 y+6 w242 h18 hwndEdit265 vws" wnumb " -E0x200 +Center -VScroll", % ws%wnumb%

		Gui, Add, Button, % "x30 y+6 w" width/2-30 " +default h27 gEditWebsites hwndBut1 vSaveBtn +Center +Default", Save
		ImageButton.Create(But1, Opt1, Opt2, "", Opt4)

		Gui, Add, Button, % "x+0 w" width/2-30 " h27 gExitWebsites hwndBut2 +Center", Cancel
		ImageButton.Create(But2, Opt1, Opt2, "", Opt4)
	}


	Gui, Add, Text, % "y+5 w" (width-14) " h2 vP"
	GuiControlGet, P, Pos
	H := PY + PH
	ShAutoComplete(Edit264)
	ShAutoComplete(Edit265)

	WinSet, Region, % "0-0 w" width*dpi " h" H*dpi " r12-12", ahk_id %WebEdit%
	If FastSearching
		Gui, Show, % "w" width " y" A_ScreenHeight/2+20, Web Edit
	else
		Gui, Show, % "w" width " y" A_ScreenHeight/2+204*amultiplier/2+20, Web Edit
	GuiControl, Focus, SaveBtn
	FadeIn(WebEdit)
return

ExitWebsites:
 	FadeOut(WebEdit)
return

EditWebsites:
	Gui, Submit, NoHide

	if FastSearching
	{
		IniDelete, keywords.ini, %currentKeyword%

		IniWrite, %newUrl%, keywords.ini, %newkeyword%, Url
		IniWrite, %newLabel%, keywords.ini, %newkeyword%, Label

		GoSub LoadCustomQueries

		url:=newUrl

		SetSearchLabelText(newLabel, webSearchVar, guiw)
	}
	else
	{
		Loop, 8
		{
			IniWrite, % ws%A_Index% , settings.ini, Web, ws%A_Index%
		}
		WebNames:= WebName1 "|" WebName2 "|" WebName3 "|" WebName4 "|" WebName5 "|" WebName6 "|" WebName7 "|" WebName8
		IniWrite, %WebNames%, settings.ini, Web, WebNames
		If ChoosingWebsite=1
		{
			WebLT:=WebLT ? false : true
			GoSub RSWeb
		}
	}
	FadeOut(WebEdit)
return

DeleteCustomQuery:
	Gui, Submit, NoHide

	If !currentKeyword
		return

	IniDelete, keywords.ini, %currentKeyword%
	url=

	GoSub LoadCustomQueries

	GuiControl, WebL:Move, webSearchVar, % "x14 w" guiw-85
	GuiControl, WebL:Hide, SearchLabel
	FastSearching:=false

	FadeOut(WebEdit)
return

StartSearch:
	Gui, Submit, NoHide

	previousSearch:=webSearchVar
	webSearchVar:=StrReplace(webSearchVar, "#", "%23")
	webSearchVar:=StrReplace(webSearchVar, "&", "%26")
	webSearchVar:=StrReplace(webSearchVar, doublequote, "")
	Loop, Parse, webSearchVar
	If (A_LoopField="." && !InStr(webSearchVar," ") && !FastSearching)
	{
		GoSub WebLClose
		Run, http:%webSearchVar%
		GoSub GetBrowserNameAndActivate
		webSearchVar=
		return
	}
	GoSub WebLClose
	if webSearchVar
	{
		if !FastSearching || !url
			url:="https://www.google.bg/search?q={query}"
		webSearchVar:=StrReplace(url, "{query}", webSearchVar)
		Run, %webSearchVar%,, UseErrorLevel
		GoSub GetBrowserNameAndActivate
		webSearchVar=
		url=
	}
return

SetSearchLabelText(newLabel = "Default:", webSearchVar = "", guiw = 540)
{
	searchLabelText := newLabel
	searchLabelMeasured := MeasureText(searchLabelText, "s12", "Segoe UI Bold")
	GuiControl, WebL:, webSearchVar, %webSearchVar%
	GuiControl, WebL:Move, webSearchVar, % "x" searchLabelMeasured.W+24 " w" guiw-(searchLabelMeasured.W+22)-65
	GuiControl, WebL:, SearchLabel, %searchLabelText%
	GuiControl, WebL:Show, SearchLabel
}